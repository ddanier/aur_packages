# Contributor: localizator <localizator@ukr.net>

# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

pkgname=seafile-client
pkgver=1.7.1
pkgrel=1
pkgdesc="Seafile is an online file storage and collaboration tool. This is seafile client for synchronizing your local files with seafile server."
arch=('i686' 'x86_64')
url="https://github.com/haiwen/seafile/"
license=('GPLv3')
depends=('ccnet>=1.3.4' 'python2-mako' 'python2-simplejson' 'python2-webpy' 'gtk2' 'libnotify' 'hicolor-icon-theme' 'libevent' 'intltool' 'seafile-common')
makedepends=()
optdepends=()
options=('!libtool' '!emptydirs')
install=seafile-client.install
source=("http://seafile.googlecode.com/files/seafile-${pkgver}.tar.gz"
        "seafile-cli.patch"
        "common-cleanup.txt")
sha256sums=('8bce8f727deebc71c04a0e1b6ad3d9d39a6454858128a0e75e1903d59fabef79'
            '31031014e887e23827677f761be62076403c7e7419f6bc6a7631f76f770747ea'
            '84db644002f3d73efa552229cf23677e118b2642b69b39f12c9232549cd78acf')

build ()
{
	cd "$srcdir/seafile-$pkgver"
	patch -p1 -i "$srcdir/seafile-cli.patch"
	./configure --disable-server --enable-client --prefix=/usr PYTHON=/usr/bin/python2
	make -j1
}

package ()
{
	cd "$srcdir/seafile-$pkgver"
	make DESTDIR="$pkgdir/" install
	install -D -m644 $srcdir/seafile-$pkgver/LICENCE.txt $pkgdir/usr/share/licenses/$pkgname/LICENCE.txt
    for F in $(<"$srcdir/common-cleanup.txt")
    do
        rm "$pkgdir/$F"
    done
}
