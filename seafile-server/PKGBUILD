pkgname=seafile-server
pkgver=1.7.1
pkgrel=1
pkgdesc="Seafile is an open-source Dropbox replacement."
arch=('i686' 'x86_64')
url="http://seafile.com"
license=('GPLv3')
depends=('glib2>=2.28'
    'libevent>=2.0'
    'curl'
    'util-linux'
    'intltool>=0.40'
    'sqlite>=3.7'
    'libmysqlclient>=5.5'
    'python2-mako'
    'python2-webpy'
    'python2-simplejson'
    'python2-imaging'
    'python2-chardet'
    'python2-pip'
    'python2-virtualenv'
    'gunicorn'
    'python2-djblets'
    'ccnet>=1.3.3'
    'libsearpc>=1.1.0'
    'libevhtp'
    'seafile-common'
    'seahub>=1.7.0'
    'django-rest-framework')
makedepends=('pacman>=4.1')
optdepends=()
provides=()
conflicts=('seafile')

options=
install=seafile-server.install

source=("https://seafile.googlecode.com/files/seafile-${pkgver}.tar.gz"
	"seafile-admin.patch"
	"seafile-server.service"
    "common-cleanup.txt")
sha256sums=('8bce8f727deebc71c04a0e1b6ad3d9d39a6454858128a0e75e1903d59fabef79'
	'e9719f910ae9939ebe2037f57e6def589320325e9dc23f0da7329fe07c279bae'
	'e90abb0ca472257cf488b82a5bc37faebc0adc87b8330f165945d2d3d03ce842'
    '84db644002f3d73efa552229cf23677e118b2642b69b39f12c9232549cd78acf')

prepare () {
	cd "$srcdir/${pkgname/-server/}-$pkgver"
	patch -p1 -i "$srcdir/seafile-admin.patch"
}

build () {
	cd "$srcdir/${pkgname/-server/}-$pkgver"
	./configure --enable-server --disable-client --prefix=/usr PYTHON=/usr/bin/python2
	make -j1
}

package () {
    # normal install
	cd "$srcdir/${pkgname/-server/}-$pkgver"
	make DESTDIR="$pkgdir/" install
    # cleanup common files
    for F in $(<"$srcdir/common-cleanup.txt")
    do
        if [ -e "$pkgdir/$F" ]
        then
            rm "$pkgdir/$F"
        fi
    done
    # install systemd service
    mkdir -p "$pkgdir/usr/lib/systemd/system/"
    cp "$srcdir/seafile-server.service" "$pkgdir/usr/lib/systemd/system/seafile-server.service"
}
